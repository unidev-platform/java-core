buildscript {
    repositories {
        jcenter()
    }
}

plugins {
    id "com.jfrog.bintray" version "1.7"
}


project.ext.springVersion="4.3.4.RELEASE"
project.ext.platformVersion = "2.1.0-SNAPSHOT"


subprojects {
    apply plugin: 'maven'
    apply plugin: 'maven-publish'
    apply plugin: 'java'
    apply plugin: 'idea'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    group = 'com.unidev.platform'
    version = "$project.platformVersion"

    if (project.hasProperty('unidevEnv')) {  //gradlew build  -PunidevEnv
        println "Unidev Envirnoment build"
        apply from: 'http://dev.local/gradle/defaultRepositories.groovy'
        apply from: 'http://dev.local/gradle/uploadArchives.groovy'
        apply from: 'http://dev.local/gradle/javadoc.groovy'
    } else {
        repositories {
            mavenCentral()
            jcenter()
        }
    }



    dependencies {

        testCompile 'org.hamcrest:hamcrest-all:1.3'
        testCompile 'junit:junit:4.12'
        testCompile 'org.mockito:mockito-all:1.10.19'
    }
    idea {
      module {
        downloadJavadoc = true
        downloadSources = true
      }
    }
    tasks.withType(JavaCompile) {
        sourceCompatibility = "1.8"
        targetCompatibility = "1.8"
    }
    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives sourcesJar
        archives javadocJar
    }

    if (project.hasProperty('travisEnv')) {

        bintray {
            user = System.getenv('BINTRAY_USER')
            key = System.getenv('BINTRAY_KEY')

            pkg {
                repo = 'unidev-platform'
                name = 'unidev-platform'
                userOrg = 'universal-development'
                licenses = ['Apache-2.0']
                vcsUrl = 'https://github.com/unidev-platform/unidev-platform.git'

                version {
                    name = version
                }
            }
        }

        publishing {
            publications {
                MyPublication(MavenPublication) {
                    from components.java
                    groupId 'com.unidev.platform'

                    version version
                }
            }
        }
    }
}
